# Maintainer: shulhan <ms@kilabit.info>

pkgname=awwan-git
pkgver=0.12.1.r0.g9586bba
pkgrel=1

pkgdesc='Configuration management software, infrastructure as file and directory layout.'
arch=('any')
url='https://awwan.org'
license=('GPL3')

makedepends=(
	'go'
	'git'
)

provides=('awwan')

source=(
	"${pkgname}::git+https://git.sr.ht/~shulhan/awwan"
	"wui::git+https://git.sr.ht/~shulhan/pakakeh.ts"
)
## For testing in local.
#source=(
#	"${pkgname}::git+file:///home/ms/go/src/git.sr.ht/~shulhan/awwan#branch=dev"
#	"wui::git+file:///home/ms/src/wui"
#)
md5sums=(
	'SKIP'
	'SKIP'
)

pkgver() {
	cd "${pkgname}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "${pkgname}"
	git submodule init
	git config submodule."submodules/wui".url "${srcdir}/wui"
	git submodule update
}

build() {
	cd "${pkgname}"
	make build
}

package() {
	cd "${pkgname}"
	install -Dm755 _bin/awwan "${pkgdir}/usr/bin/awwan"
	install -Dm755 COPYING    "${pkgdir}/usr/share/licenses/awwan/COPYING"
	install -Dm644 _sys/usr/share/bash-completion/completions/awwan \
		 "${pkgdir}/usr/share/bash-completion/completions/awwan"
}
